TARGET = prl_disp_service
PROJ_PATH = $$PWD
include(../Build/qmake/build_target.pri)

LIBS += -lprlcommon -lprl_xml_model -lboost_chrono -lvcmmd

linux-g++* {
	system(ld -lboost_thread-mt) {
		message( boost-with-mt )
		CONFIG += boost-with-mt
	} else {
		system(ld -lboost_thread) {
			message( boost-without-mt )
			CONFIG += boost-without-mt
		}
	}
}

include($$LIBS_LEVEL/HostInfo/CHostInfo.pri)
include($$LIBS_LEVEL/DispToDispProtocols/DispToDispProtocols.pri)
include($$LIBS_LEVEL/PrlNetworking/PrlNetworking.pri)
include($$LIBS_LEVEL/ProblemReportUtils/ProblemReportUtils.pri)
include($$LIBS_LEVEL/PrlCommonUtils/PrlCommonUtils.pri)
include($$LIBS_LEVEL/CAuth/CAuth.pri)
include($$LIBS_LEVEL/VmFileList/VmFileList.pri)
include($$LIBS_LEVEL/Virtuozzo/Virtuozzo.pri)
include($$LIBS_LEVEL/StatesUtils/StatesUtils.pri)
include($$LIBS_LEVEL/StatesStore/StatesStore.pri)
include($$LIBS_LEVEL/Buse/Buse.pri)
include($$LIBS_LEVEL/CpuFeatures/CpuFeatures.pri)

include($$LIBS_LEVEL/PowerWatcher/PowerWatcher.pri)
win32: include($$LIBS_LEVEL/BattWatcher/BattWatcher.pri)
win32 {
	include($$LIBS_LEVEL/WinUAC/ElevatedPriveleges/ElevatedPriveleges.pri)
	include($$LIBS_LEVEL/WinSecurityChecks/SigningValidity/SigningValidity.pri)
	include($$LIBS_LEVEL/WinDbgUtils/WinDbgUtils.pri)
}
libvirt {
	include($$LIBS_LEVEL/Transponster/Transponster.pri)
	LIBS += -lvirt -lvirt-qemu
}
boost-with-mt {
	LIBS += -lboost_thread-mt -lboost_filesystem-mt -lboost_system-mt
}
boost-without-mt {
	LIBS += -lboost_thread -lboost_filesystem -lboost_system
}
